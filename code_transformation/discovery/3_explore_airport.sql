SELECT GEO_CODE, COUNTRY_NAME, COUNTRY_CODE, CITY_NAME, CITY_CODE, AIRPORT_CODE, AIRPORT_NAME
FROM
    OPENROWSET(
        BULK 'airport_ref.json',
        DATA_SOURCE = 'flights_data',
        FORMAT = 'CSV',
        FIELDTERMINATOR = '0x0b',
        FIELDQUOTE = '0x0b',
        ROWTERMINATOR = '0x0b'
    ) 
    WITH (
        jsonDoc VARCHAR(MAX)
    ) AS GEO_CODE
    CROSS APPLY OPENJSON(jsonDoc)
    WITH (
        GEO_CODE NVARCHAR(MAX) '$.GEO_CODE',
        country NVARCHAR(MAX) '$.COUNTRY_LIST' AS JSON 
    )
    CROSS APPLY OPENJSON(country)
    WITH (
        COUNTRY_NAME NVARCHAR(MAX) '$.COUNTRY_NAME',
        COUNTRY_CODE NVARCHAR(MAX) '$.COUNTRY_CODE',
        city NVARCHAR(MAX) '$.CITY_LIST' AS JSON 
    )
    CROSS APPLY OPENJSON(city)
    WITH (
        CITY_NAME NVARCHAR(MAX) '$.CITY_NAME',
        CITY_CODE NVARCHAR(MAX) '$.CITY_CODE',
        airport NVARCHAR(MAX) '$.AIRPORT_LIST' AS JSON 
    )
    CROSS APPLY OPENJSON(airport)
    WITH (
        AIRPORT_CODE NVARCHAR(MAX) '$.AIRPORT_CODE',
        AIRPORT_NAME NVARCHAR(MAX) '$.AIRPORT_NAME'
    );


-- Cr√©ation de la vue
CREATE OR ALTER VIEW airport_View AS
SELECT 
    GEO_CODE COLLATE Latin1_General_100_CI_AS_SC_UTF8 AS GEO_CODE,
    COUNTRY_NAME COLLATE Latin1_General_100_CI_AS_SC_UTF8 AS COUNTRY_NAME,
    COUNTRY_CODE COLLATE Latin1_General_100_CI_AS_SC_UTF8 AS COUNTRY_CODE,
    CITY_NAME COLLATE Latin1_General_100_CI_AS_SC_UTF8 AS CITY_NAME,
    CITY_CODE COLLATE Latin1_General_100_CI_AS_SC_UTF8 AS CITY_CODE,
    AIRPORT_CODE COLLATE Latin1_General_100_CI_AS_SC_UTF8 AS AIRPORT_CODE,
    AIRPORT_NAME COLLATE Latin1_General_100_CI_AS_SC_UTF8 AS AIRPORT_NAME
FROM
    OPENROWSET(
        BULK 'airport_ref.json',
        DATA_SOURCE = 'flights_data',
        FORMAT = 'CSV',
        FIELDTERMINATOR = '0x0b',
        FIELDQUOTE = '0x0b',
        ROWTERMINATOR = '0x0b'
    ) 
    WITH (
        jsonDoc VARCHAR(MAX)
    ) AS raw
    CROSS APPLY OPENJSON(jsonDoc)
    WITH (
        GEO_CODE VARCHAR(50) COLLATE Latin1_General_100_CI_AS_SC_UTF8 '$.GEO_CODE',
        country NVARCHAR(MAX) '$.COUNTRY_LIST' AS JSON
    )
    CROSS APPLY OPENJSON(country)
    WITH (
        COUNTRY_NAME VARCHAR(200) COLLATE Latin1_General_100_CI_AS_SC_UTF8 '$.COUNTRY_NAME',
        COUNTRY_CODE VARCHAR(10) COLLATE Latin1_General_100_CI_AS_SC_UTF8 '$.COUNTRY_CODE',
        city NVARCHAR(MAX) '$.CITY_LIST' AS JSON
    )
    CROSS APPLY OPENJSON(city)
    WITH (
        CITY_NAME VARCHAR(200) COLLATE Latin1_General_100_CI_AS_SC_UTF8 '$.CITY_NAME',
        CITY_CODE VARCHAR(50) COLLATE Latin1_General_100_CI_AS_SC_UTF8 '$.CITY_CODE',
        airport NVARCHAR(MAX) '$.AIRPORT_LIST' AS JSON
    )
    CROSS APPLY OPENJSON(airport)
    WITH (
        AIRPORT_CODE VARCHAR(50) COLLATE Latin1_General_100_CI_AS_SC_UTF8 '$.AIRPORT_CODE',
        AIRPORT_NAME VARCHAR(200) COLLATE Latin1_General_100_CI_AS_SC_UTF8 '$.AIRPORT_NAME'
    );

-- select * 
-- from airport_View